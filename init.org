#+TITLE: Emacs configuration
#+PROPERTY: header-args :tangle ./init.el

* Introduction
  :PROPERTIES:
  :header-args: :tangle no
  :END:

  Here is the source of my Emacs configuration. Create the files by
  saving by calling [[elisp:(compile "make setup")][make setup]].

* Emacs setup

  In this section, I keep track of the way I install Emacs on various
  environments.

** Elementary

   Download the tar sources, then

   #+BEGIN_SRC sh :tangle no
     sudo apt install libjansson-dev libtree-sitter-dev
     ./configure --with-modules --tree-sitter
     make
     sudo make install
   #+END_SRC

** Mac OS
:PROPERTIES:
:header-args: :tangle no
:END:
*** From scratch
**** Dependencies

-lemutls_w: https://github.com/d12frosted/homebrew-emacs-plus/issues/554

#+begin_src emacs-lisp
  (setenv "LIBRARY_PATH"
          (string-join
           '("/opt/homebrew/opt/gcc/lib/gcc/13"
             "/opt/homebrew/opt/libgccjit/lib/gcc/13"
             "/opt/homebrew/opt/gcc/lib/gcc/13/gcc/aarch64-apple-darwin22/13")
           ":"))
#+end_src

#+begin_src sh
  brew install imagemagick libgccjit
#+end_src

**** Install

Clone repo

#+begin_src sh :async
  git clone git://git.savannah.gnu.org/emacs.git .emacs-build
#+end_src

Install

#+begin_src sh :dir (expand-file-name ".emacs-build" (file-name-directory buffer-file-name)) :async
  make configure
  ./configure --with-imagemagick --with-json --with-tree-sitter --with-xwidgets --with-native-compilation
#+end_src

*** From the brew formula

#+BEGIN_SRC sh
  brew tap railwaycat/emacsmacport
  brew install emacs-mac --with-xwidgets --with-natural-title-bar --with-librsvg
  ln -s /usr/local/opt/emacs-mac/Emacs.app ~/Applications
#+END_SRC

* Package management

I use the default package manager with ~use-package~.

Setup ~vc-use-package~ too:

#+begin_src emacs-lisp
  (require 'package)

  (add-to-list 'package-archives '("melpa" . "https://melpa.org/packages/") t)

  (package-initialize)
#+end_src

* Main script
** Global variables

   These are common useful variables for getting the emacs init dir
   and the path to my personal local packages.

   #+BEGIN_SRC emacs-lisp
     (eval-and-compile
       (defconst my-init-dir (file-name-directory (or load-file-name (buffer-file-name))))
       (defconst my-site-lisp (concat my-init-dir "site-lisp/"))
       (add-to-list 'load-path my-site-lisp))
   #+END_SRC

** Custom configuration

   Move the custom configuration file outside of the init file to
   avoid blending custom configuration with the init sources.

   #+BEGIN_SRC emacs-lisp
     (setq custom-file (concat my-init-dir "custom-file.el"))
     (load custom-file 'no-error)
   #+END_SRC

* Theme
** Default settings

   Remove GUI garbage and increase the font size.

   #+BEGIN_SRC emacs-lisp
     (tool-bar-mode 0)
     (set-face-attribute 'default nil :height 140 :family "Source code pro")
   #+END_SRC

** Theme

#+NAME: my-themes
- tango-dark
- deeper-blue

#+begin_src emacs-lisp :var themes=my-themes
  (load-theme (intern (nth (random (length themes)) themes)))
#+end_src

** Mode line
*** Powerline

    I am using my own theme forked from ~powerline-default-theme~.

    #+BEGIN_SRC emacs-lisp
      (defface my-mode-line-highlight
        '((t :inherit mode-line
             :background "gray32"))
        "Mode line face for highlighted elements")

      (defface my-mode-line-highlight-active
        '((t :inherit 'mode-line
             :background "gray50"))
        "Mode line face for active highlighted elements")

      (defun my-powerline-theme ()
        "Setup the default mode-line."
        (interactive)
        (setq-default
         mode-line-format
         '("%e"
           (:eval
            (let* ((active (powerline-selected-window-active))
                   (mode-line-buffer-id (if active 'mode-line-buffer-id 'mode-line-buffer-id-inactive))
                   (mode-line (if active 'mode-line 'mode-line-inactive))

                   (lhs (list (powerline-raw
                               (if buffer-read-only "RO " "RW ")
                               (if (buffer-modified-p) 'my-mode-line-highlight-active 'my-mode-line-highlight)
                               'l)
                              (when (> (length (tab-bar-tabs)) 1)
                                (powerline-raw (thread-last (tab-bar-tabs)
                                                            (assq 'current-tab)
                                                            (assq 'name)
                                                            cdr)
                                               mode-line 'l))
                              (powerline-buffer-id `(mode-line-buffer-id ,mode-line) 'l)
                              (powerline-raw " " mode-line)
                              (powerline-process mode-line)
                              (powerline-narrow mode-line 'l)
                              (powerline-raw " " mode-line)
                              ))
                   (rhs (list
                         (powerline-vc mode-line 'r)
                         (powerline-raw (when flymake-mode (flymake--mode-line-counters)))
                         (unless window-system
                           (powerline-raw (char-to-string #xe0a1) mode-line 'l))
                         )))

              (concat (powerline-render lhs)
                      (powerline-fill mode-line (powerline-width rhs))
                      (powerline-render rhs)))))))
    #+END_SRC

    #+BEGIN_SRC emacs-lisp
      (use-package powerline
        :ensure t
        :config
        (setq powerline-default-separator 'bar
              powerline-display-hud nil)
        (my-powerline-theme))
    #+END_SRC

** Emojis

   ~emojify~ displays ascii emojis using images. This is both prettier
   and faster to render.

   #+begin_src emacs-lisp
     (use-package emojify
       :ensure t
       :hook (prog-mode . emojify-mode)
       :custom (emojify-emoji-styles '(unicode)))
   #+end_src

* General UX
** Startup screen

   Use my personal startup file instead of the default one.

   #+begin_src emacs-lisp
     (setq initial-buffer-choice (expand-file-name "welcome.org" my-init-dir))
   #+end_src

** Ivy

   I use [[https://github.com/abo-abo/swiper][ivy]] instead of the basic read interface, because it has a
   good matching system and is lighter than helm.

   Ivy comes with counsel and swiper, that implements a lot of common
   Emacs commands with the Ivy interface.

   ~ivy-use-virtual-buffers~ also includes recent files and bookmarks
   in counsel's buffer list.

   ~enable-recursive-minibuffers~ is not directly related to ivy, but
   I set it up here as most of my interactions with the minibuffer
   goes through ivy. It allows opening a new minibuffer while a
   minibuffer is already opened, which I used at my job for finding
   information on my current task while creating branches, for
   example.

   #+BEGIN_SRC emacs-lisp
     (defun init/setup-ivy ()
       "Setup the ivy package."
       (ivy-mode 1)
       (setq ivy-use-virtual-buffers t)
       (setq enable-recursive-minibuffers t))

     (use-package ivy
       :ensure t
       :config (init/setup-ivy))

     (use-package counsel
       :ensure t
       :after (ivy)
       :config (counsel-mode 1))

     (use-package swiper
       :ensure t
       :after (ivy)
       :bind (("C-s" . swiper)))
   #+END_SRC

** Subword

   Using subword-mode is more convenient in PascalCase / camelCase languages

   #+begin_src emacs-lisp
     (use-package subword
       :hook (prog-mode . subword-mode))
   #+end_src

** Prompts

   Use =y-or-n-p= instead of =yes-or-no-p= to have a smoother experience.

   #+begin_src emacs-lisp
     (defalias 'yes-or-no-p 'y-or-n-p)
   #+end_src

** Helpful

   [[https://github.com/Wilfred/helpful][helpful]] improves the emacs help commands with more information.

   #+begin_src emacs-lisp
     (use-package helpful
       :ensure t
       :bind
       ("C-h k" . helpful-key)
       ("C-c C-d" . helpful-at-point)
       ("C-h C" . helpful-command)
       ("C-h o" . helpful-symbol)
       :custom
       (counsel-describe-function-function #'helpful-callable)
       (counsel-describe-variable-function #'helpful-variable))
   #+end_src

* Performances

  I use [[https://github.com/jschaf/esup][esup]] to profile my emacs startup from time to time.

  #+begin_src emacs-lisp
    (use-package esup
      :ensure t
      :commands (esup)
      :init (setq esup-depth 0))
  #+end_src

  [[https://github.com/emacsmirror/gcmh][gcmh]] minimizes the interferences of the garbage collector with the
  user's activity. There are more details on the package's page.

  #+BEGIN_SRC emacs-lisp
    (use-package gcmh
      :ensure t
      :config (gcmh-mode 1))
  #+END_SRC

* Editing
** French keyboard setup

   I use an AZERTY keyboard, which requires loading ~iso-transl~ to
   support all its keys.

   #+BEGIN_SRC emacs-lisp
     (use-package iso-transl)
   #+END_SRC

** Mac special setup

   Rebind some MacOS keys to have proper super of control, alt gr,
   etc...

   #+begin_src emacs-lisp
     (when (eq system-type 'darwin)
       (setq mac-option-modifier 'meta
	     mac-right-option-modifier nil
	     mac-command-modifier 'super))
   #+end_src

** Parentheses

   Enable some core modes in order to get electric pairing and showing
   the parenthesis matching the one under the cursor.

   #+begin_src emacs-lisp
     (electric-pair-mode 1)
     (show-paren-mode 1)
   #+end_src

** Auto completion

   #+begin_src emacs-lisp
     (use-package company
       :ensure t
       :init
       (global-campany-mode))
   #+end_src

** Code checking

   #+begin_src emacs-lisp
     (use-package flymake
       :ensure
       :hook (prog-mode . flymake-mode)
       :bind ((:map flymake-mode-map
		    ("C-c ! l" . flymake-show-buffer-diagnostics)
		    ("C-c ! p" . flymake-goto-prev-error)
		    ("C-c ! n" . flymake-goto-next-error))))
   #+end_src

** Auto formatting

   I basically never want trailing whitespaces

   #+begin_src emacs-lisp
     (add-hook 'before-save-hook #'delete-trailing-whitespace)
   #+end_src

   I use [[https://editorconfig.org/][editorconfig]] as much as possible so that I can share part my
   project config with my teammates.

   #+begin_src emacs-lisp
     (use-package editorconfig
       :ensure t
       :if (locate-library "editorconfig")
       :hook (prog-mode . editorconfig-mode))
   #+end_src

** Tree sitter

   #+begin_src emacs-lisp
     (use-package tree-sitter
       :ensure t)
   #+end_src

   #+begin_src emacs-lisp
     (use-package tree-sitter-langs
       :ensure t
       :hook ((php-mode . tree-sitter-hl-mode)
              (js-mode . tree-sitter-hl-mode)
              (typescript-mode . tree-sitter-hl-mode)))
   #+end_src

** Backups

   Stop having backups files inside my projects and committing them by
   mistake.

   #+begin_src emacs-lisp
     (setq backup-directory-alist
	   `((".*" . ,temporary-file-directory)))
     (setq auto-save-file-name-transforms
	   `((".*" ,temporary-file-directory t)))
   #+end_src

** Starcoder

#+begin_src emacs-lisp
  (use-package starhugger
    :ensure t
    :bind (("C-c <tab>" . starhugger-trigger-suggestion)
           :map starhugger-inlining-mode-map
           ("<M-return>" . starhugger-accept-suggestion)
           ("<M-S-down>" . starhugger-show-next-suggestion)
           ("<M-S-up>" . starhugger-show-next-suggestion)))
#+end_src

** Devdocs

#+begin_src emacs-lisp
  (use-package devdocs
    :ensure t
    :commands (devdocs-install)
    :bind (("C-c C-h" . devdocs-peruse)))
#+end_src

** Windmove

#+begin_src emacs-lisp
  (use-package windmove
    :config (windmove-default-keybindings))
#+end_src

* Navigation
** imenu

   #+BEGIN_SRC emacs-lisp
     (global-set-key (kbd "C-c i") #'imenu)
   #+END_SRC

** Treemacs

   [[https://github.com/Alexander-Miller/treemacs][Treemacs]] is a nice tree layout file explorer for Emacs.

   #+BEGIN_SRC emacs-lisp
     (use-package treemacs
       :ensure t
       :commands (treemacs)
       :bind (("<f5>" . treemacs)))
   #+END_SRC

** ripgrep

   Ripgrep is my preferred way to search for occurences in a
   project. It is fast, and [[https://github.com/Wilfred/deadgrep][deadgrep]] offers a really nice interface
   for Emacs.

   #+BEGIN_SRC emacs-lisp
     (use-package deadgrep
       :ensure t
       :bind (("C-c C-s" . deadgrep)))
   #+END_SRC

* Project management
** git

   Use magit, OF COURSE

   #+begin_src emacs-lisp
     (use-package magit
       :ensure t
       :commands (magit-status))
   #+end_src

** Project

#+begin_src emacs-lisp
  (use-package project)
#+end_src

** Task runner

#+begin_src emacs-lisp
  (use-package task-runner
    :bind ("<f4>" . task-runner-run-task))
#+end_src

** Test watcher

#+begin_src emacs-lisp
  (use-package test-watcher)
#+end_src

* Shell
** Environment variables

   Use [[https://github.com/purcell/exec-path-from-shell][exec-path-from-shell]] to import shell's environment variables
   into Emacs.

   #+begin_src emacs-lisp
     (use-package exec-path-from-shell
       :ensure t
       :custom ((exec-path-from-shell-variables '("PATH" "MANPATH" "NODE_OPTIONS")))
       :config (exec-path-from-shell-initialize))
   #+end_src

** xterm-color

   [[https://github.com/atomontage/xterm-color][xterm-color]] is a replacement for ansi-color that is faster and has
   more feature.

   Here is the comint / shell-mode configuration

   #+BEGIN_SRC emacs-lisp
     (defun my-remove-ansi-from-comint ()
       "Remove ansi-color from comint filters."
       (setq comint-output-filter-functions
	   (remove 'ansi-color-process-output comint-output-filter-functions)))


     (defun my-shell-mode-config-xterm-color ()
       "Configure xterm-color for shell-mode."
       ;; Disable font-locking in this buffer to improve performance
       (font-lock-mode -1)
       ;; Prevent font-locking from being re-enabled in this buffer
       (make-local-variable 'font-lock-function)
       (setq font-lock-function (lambda (_) nil))
       (setq comint-output-filter-functions
         (remove 'ansi-color-process-output comint-output-filter-functions))
       (add-hook 'comint-preoutput-filter-functions 'xterm-color-filter nil t)
       (setq-local comint-terminfo-terminal "xterm-256color"))
   #+END_SRC

   Then, we configure eshell:

   #+BEGIN_SRC emacs-lisp
     (defun my-eshell-before-prompt-xterm-color ()
       "Preserve text properties on eshell prompts."
       (setq xterm-color-preserve-properties t))

     (defun my-eshell-env-xterm-color ()
       "Setup eshell environment for xterm-color."
       (setenv "TERM" "xterm-256color"))
   #+END_SRC

   And compilation-mode:

   #+BEGIN_SRC emacs-lisp
     (defun my-xterm-color-configure-compilation ()
       "Setup xterm-color in compilation-mode"
       (message "Loading xterm-colors for compilation")
       (with-eval-after-load 'compile
	 (setq compilation-environment '("TERM=xterm-256color"))

	 (add-hook 'compilation-start-hook
		   (lambda (proc)
		     ;; We need to differentiate between compilation-mode buffers
		     ;; and running as part of comint (which at this point we assume
		     ;; has been configured separately for xterm-color)
		     (when (eq (process-filter proc) 'compilation-filter)
		       ;; This is a process associated with a compilation-mode buffer.
		       ;; We may call `xterm-color-filter' before its own filter function.
		       (set-process-filter
			proc
			(lambda (proc string)
			  (funcall 'compilation-filter proc
				   (xterm-color-filter string)))))))))

   #+END_SRC

   Finally, we can import and configure the package:

   #+BEGIN_SRC emacs-lisp
     (defun my-xterm-color-init ()
       "First setup for xterm-color."
       (my-remove-ansi-from-comint)
       (my-xterm-color-configure-compilation))

     (use-package xterm-color
       :ensure t
       :config (my-xterm-color-init)
       :hook ((shell-mode . my-shell-mode-config-xterm-color)
              (eshell-mode . my-eshell-env-xterm-color)
              (eshell-before-prompt . my-eshell-before-prompt-xterm-color)
              (compilation-mode . my-shell-mode-config-xterm-color)))
   #+END_SRC

** vterm

   #+begin_src emacs-lisp
     (use-package vterm
       :ensure t
       :no-require t
       :commands (vterm))
   #+end_src

* Org mode
** Basic configuration
*** Clock table indentation

    The org clock table indents its entries using the LateX symbol
    ~\emsp~, which renders badly in org buffers. I override it with my
    own indent function extracted from [[https://emacs.stackexchange.com/questions/9528/is-it-possible-to-remove-emsp-from-clock-report-but-preserve-indentation][a stackexchange discussion]].

    #+BEGIN_SRC emacs-lisp
      (defun my/org-clocktable-indent-string (level)
        (if (= level 1)
            ""
          (let ((str "+"))
            (while (> level 2)
              (setq level (1- level)
                    str (concat str "--")))
            (concat str "-> "))))
    #+END_SRC

*** Org initialization

    #+BEGIN_SRC emacs-lisp
      (defun my/init-org ()
        ;; Override clock table ident function with mine
        (advice-add 'org-clocktable-indent-string :override #'my/org-clocktable-indent-string)

        ;; Automatically add syntax coloration on org src blocks
        (setq org-src-fontify-natively t)

        (setq org-hide-emphasis-markers t)

        (add-hook 'org-mode-hook #'(lambda () (org-indent-mode t)))

        (org-babel-do-load-languages 'org-babel-load-languages
                                     '((shell . t)
                                       (sql . t)))

        ;; Allow using top-level await in js code blocks
        (setq org-babel-js-function-wrapper "(async function(){%s
        })().then(result => require('process').stdout.write(require('util').inspect(result, { maxArrayLength: Infinity})));"))

    #+END_SRC

*** Package declaration

   #+BEGIN_SRC emacs-lisp
     (use-package org
       :mode ("\\.org\\'" . org-mode)
       :bind (("C-c o t" . org-todo-list))
       :config (my/init-org)
       :custom
       (org-startup-folded t "Start all org documents in overview mode"))
   #+END_SRC

** Mouse

#+begin_src emacs-lisp
  (use-package org-mouse
    :after (org))
#+end_src

** Agenda

   #+BEGIN_SRC emacs-lisp
     (use-package org-agenda
       :bind (("C-c o a" . org-agenda-list)))
   #+END_SRC

** Clock

   #+BEGIN_SRC emacs-lisp
     (use-package org-clock
       :bind (("C-c o j" . org-clock-goto)))
   #+END_SRC

** Capture

   #+BEGIN_SRC emacs-lisp
     (use-package org-capture
       :bind (("C-c o c" . org-capture)))
   #+END_SRC

** Async

   #+BEGIN_SRC emacs-lisp
     (use-package ob-async
       :no-require t
       :after (org))
   #+END_SRC
* Lisp

 #+begin_src emacs-lisp
   (use-package elisp-mode
     :bind (:map emacs-lisp-mode-map
		 ("C-c C-b" . eval-buffer)))
 #+end_src

* LSP (eglot)

  #+begin_src emacs-lisp
    (defcustom my-eglot-typescript-args '()
      ""
      :safe t)

    (use-package eglot
      :bind (:map eglot-mode-map
                  ("C-c SPC" . eglot-code-actions))
      :init
      ;; Unless I update my emacs, add a polyfill for project-name
      ;; (unless (fboundp 'project-name)
    ;;     (cl-defgeneric project-name (project)
    ;;       "A human-readable name for the project.
    ;; Nominally unique, but not enforced."
    ;;       (file-name-nondirectory (directory-file-name (project-root project)))))

      :config
      (add-to-list 'eglot-server-programs
                   `((js-mode typescriptreact-mode typescript-mode) .
                     ("typescript-language-server"
                      "--stdio"
                      :initializationOptions
                      (:preferences (:includeInlayParameterNameHints "none"
                                     :includeInlayPropertyDeclarationTypeHints t
                                     :includeInlayFunctionLikeReturnTypeHints t)
                                    :plugins [(:name "typescript-eslint-language-service"
                                                     :location ,(expand-file-name "node_modules/typescript-eslint-language-directory" user-emacs-directory))])))))
  #+end_src

* Sonarlint

  #+begin_src emacs-lisp :tangle no
    (autoload 'sonar-visit-file-page "sonar" nil t)
  #+end_src

* Web

  Use [[http://web-mode.org/][web-mode]] for editing HTML files

  #+begin_src emacs-lisp
    (use-package web-mode
      :ensure t
      :mode "\\.html\\'")
  #+end_src

  Use [[https://elpa.gnu.org/packages/rainbow-mode.html][rainbow-mode]] to get a preview of the hexa / rgb color we are
  reading.

  #+begin_src emacs-lisp
    (use-package rainbow-mode
      :ensure t
      :hook (js-mode css-mode web-mode))
  #+end_src

* Javascript
** Eglot

   #+BEGIN_SRC emacs-lisp
     (use-package js
       :mode (("\\.mjs\\'" . js-mode))
       :bind (:map js-mode-map
			("M-." . xref-find-definitions))
       :config
       (add-hook 'js-mode-hook #'eglot-ensure))
   #+END_SRC

** Typescript

   Setup the basic typescript-mode:

   #+BEGIN_SRC emacs-lisp
     (use-package typescript-mode
       :ensure t
       :mode (("\\.ts\\'" . typescript-mode)
              ("\\.tsx\\'" . typescriptreact-mode))
       :config
       ;; Eglot uses the major mode name as the languageId to send to the LSP server.
       ;; However, typescript-language-server has a different langaugeId for typescript
       (define-derived-mode typescriptreact-mode typescript-mode
         "Typescript TSX")

       (add-to-list 'tree-sitter-major-mode-language-alist '(typescriptreact-mode . tsx))
       (add-hook 'typescript-mode-hook #'eglot-ensure))
   #+END_SRC

** Eslint

   #+begin_src emacs-lisp
     (defun my-eslint-fix ()
       "Run eslint --fix on the current buffer"
       (interactive)
       (let ((default-directory (project-root (project-current))))
	 (async-shell-command (format "npx eslint --fix %s" (buffer-file-name)))))
   #+end_src

** JSON

   #+BEGIN_SRC emacs-lisp
     (use-package json-ts-mode
       :commands (json-ts-mode)
       :mode "\\.json\\'")
   #+END_SRC

** NVM

   Setup the correct node version when opening a JS file.

   #+BEGIN_SRC emacs-lisp :tangle no
     (defun my-nvm-use-for ()
       (interactive)
       (condition-case error
           (nvm-use-for-buffer)
         (t (message "NVM error: %s" error))))

     (use-package nvm
       :ensure t
       :hook ((js-mode json-mode typescript-mode dired-after-readin magit-mode) . my-nvm-use-for))
   #+END_SRC

** Swagger

   Setup a custom command to be able to edit yaml in multi-line comments.

   #+BEGIN_SRC emacs-lisp
     (use-package yaml-comment
       :after (typescript-mode)
       :bind (:map js-mode-map
	      ("C-c y" . yaml-comment-edit-at-point)
	      :map typescript-mode-map
	      ("C-c y" . yaml-comment-edit-at-point)))
   #+END_SRC

** Prettier

   Enable prettier formatting at save for all the web files.

   #+begin_src emacs-lisp
     (use-package prettier-js
       :ensure t
       :hook ((js-mode . prettier-js-mode)
              (typescript-mode . prettier-js-mode)
              (web-mode . prettier-js-mode)
              (css-mode . prettier-js-mode))
       :custom ((prettier-js-show-errors . nil)))
   #+end_src

** Node modules support

   ~add-node-modules-path~ automatically adds the node_modules bin
   folder to the path. This allows using the project tools when
   opening a file (ex: eslint, prettier).

   Make sure to add the hooks as late as possible, as some other
   packages relies on it.

   #+BEGIN_SRC emacs-lisp
     (use-package add-node-modules-path
       :ensure t
       :hook ((js-mode . add-node-modules-path)
              (typescript-mode . add-node-modules-path)))
   #+END_SRC

** Comint extras

#+begin_src emacs-lisp
  (autoload 'comint-extras-node-repl "comint-extras" "" t)
#+end_src

* PHP
** php-mode

   #+BEGIN_SRC emacs-lisp :tangle no
     (use-package php-mode
       :mode "\\.php\\'")
   #+END_SRC

** eglot

   This package requires [[https://github.com/felixfbecker/php-language-server][php-language-server]] to work. Follow the
   instructions on the readme to do so.

   #+BEGIN_SRC emacs-lisp :tangle no
     (use-package eglot
       :hook ((php-mode . eglot-ensure)))
   #+END_SRC
* Docker
** dockerfile-mode

   #+BEGIN_SRC emacs-lisp
     (use-package dockerfile-mode
       :ensure t)
   #+END_SRC

** docker

   #+BEGIN_SRC emacs-lisp
     (use-package docker
       :ensure t
       :commands (docker))
   #+END_SRC

** TRAMP

#+begin_src emacs-lisp
  (use-package tramp-container
    :after (tramp))
#+end_src

* Markdown

  #+BEGIN_SRC emacs-lisp
    (use-package markdown-mode
      :ensure t
      :mode "\\.md\\'")
  #+END_SRC

* YAML

  #+BEGIN_SRC emacs-lisp
    (use-package yaml-mode
      :ensure t)
  #+END_SRC

* TRAMP

  Make sure the remote PATH will be properly set when connecting with
  tramp on SSH:

  #+BEGIN_SRC emacs-lisp
    (with-eval-after-load 'tramp
      (add-to-list 'tramp-remote-path 'tramp-own-remote-path))
  #+END_SRC

* Project libs

  Load project libraries that are in the ~projects~ folder. These are
  not committed as it depends on the machine.

  #+BEGIN_SRC emacs-lisp
    (let ((projects-dir (concat my-site-lisp "projects/")))
      (message projects-dir)
      (dolist (lib (directory-files projects-dir t "\.el$"))
	(load-file lib)))
  #+END_SRC

* Processing

  #+begin_src emacs-lisp :tangle no
    (autoload 'p5js-start-for-buffer "p5js" nil t)
  #+end_src

* Rest

  #+begin_src emacs-lisp
    (use-package restclient
      :ensure t
      :commands restclient-mode)
  #+end_src

* GraphQL

#+begin_src emacs-lisp
  (use-package graphql-mode
    :ensure t)
#+end_src

* Termux

  Configuration for termux environment

  #+begin_src emacs-lisp
    (when (getenv "ANDROID_DATA")
      (xterm-mouse-mode 1)
      (global-set-key (kbd "<mouse-5>") #'next-line)
      (global-set-key (kbd "<mouse-4>") #'previous-line))
  #+end_src

* LLM

#+begin_src emacs-lisp
  (use-package gptel
    :ensure t
    :commands (gptel)
    :bind (("C-c g m" . gptel-menu)
           ("C-c g r" . gptel-rewrite-menu))

    :init
    (setq gptel-backend
          (gptel-make-anthropic "Claude"
            :key (auth-source-pick-first-password :host "anthropic"))
          gptel-model "claude-3-haiku-20240307"))
#+end_src

* Mongo

A function that inserts a random ObjectId in the current buffer for
easily writing mocks and tests.

#+begin_src emacs-lisp
  (defun my-insert-object-id ()
    "Write a random ObjectId at the current position."
    (interactive)
    (insert (let ((hex "0123456789abcdef"))
              (mapconcat (lambda (_) (string (aref hex (random 16))))
                         (number-sequence 1 24)
                         ""))))
#+end_src

* Local variables

# Local Variables:
# after-save-hook: org-babel-tangle
# End:
